plugins {
    id 'com.github.johnrengelman.shadow' version '7.+'
    id 'com.modrinth.minotaur' version '2.+'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    modApi "dev.architectury:architectury-fabric:${project.architectury_version}"
    common(project(path: ":playerpets-1.19.4:common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":playerpets-1.19.4:common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier 'dev'
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'playerpets'
    versionName = '[1.19.4 Fabric]'
    versionNumber = project.version
    versionType = 'alpha'
    uploadFile = remapJar
    gameVersions = ['1.19.4']
    loaders = ['fabric', 'quilt']
    dependencies {
        required.project 'architectury-api'
    }
    changelog = new File(rootProject.projectDir, 'changelog.md').text.replace('%version%', project.version)
}